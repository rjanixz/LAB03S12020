options {
  IGNORE_CASE = true;
  STATIC = false;
}

PARSER_BEGIN(Gramatica)

package lab03;

import lab03.instr.*;
import lab03.exp.*;

import java.util.ArrayList;
import java.util.List;


public class Gramatica {

    List<Instruccion> instrucciones = new ArrayList();
}
PARSER_END(Gramatica)

/** Lexico */
SKIP : {
      " "
    | "\t"
    | "\r"
    | "\n"
}

TOKEN : {
      <NUM: (   ["0"-"9"])+ >
    | <VAR: "var">
    | <IMPRIMIR: "imprimir">
    | <BREAK: "break">
    | <CONTINUE: "continue">
    | <IF: "if">
    | <ELSE: "else">
    | <WHILE: "while">
    | <ID: ["a"-"z"] ( ["a"-"z"] | ["0"-"9"] | "_")* >
    | <PCOMA: ";">
    | <PARENI: "(">
    | <PAREND: ")">
    | <LLAVEI: "{">
    | <LLAVED: "}">
    | <MAS: "+">
    | <MENOS: "-">
    | <POR: "*">
    | <DIV: "/">
    | <GT: ">">
    | <LT: "<">
    | <EQ: "=">
}
/** Fin Lexico */

void start() :
{ Instruccion instr; }
{
    (instr = instruccion() {instrucciones.add(instr);} )+ <EOF>
}

Instruccion instruccion() :
{ Instruccion instr; }
{
    (
        instr = definicion()
        |
        instr = asignacion()
        |
        instr = imprimir()
        |
        instr = saltar()
        |
        instr = continuar()
        |
        instr = si()
        |
        instr = mientras()
    )
    {
        return instr;
    }
}

Definicion definicion() :
{
    String id;
}
{
    <VAR> <ID> { id = token.image; } <PCOMA>

    {
        return new Definicion(id);
    }
}

Asignacion asignacion() :
{
    String id;
    Expresion exp;
}
{
    <ID> { id = token.image; } <EQ> exp = exp() <PCOMA>
    {
        return new Asignacion(id, exp);
    }
}


Imprimir imprimir() :
{
    Expresion exp;
}
{
    <IMPRIMIR> <PARENI> exp = exp() <PAREND> <PCOMA>
    {
        return new Imprimir(exp);
    }
}

Break saltar() :
{ }
{
    <BREAK> <PCOMA> { return new Break();}
}

Continue continuar() :
{ }
{
    <CONTINUE> <PCOMA> { return new Continue();}
}

If si() :
{
    ExpBoolean expBool;
    Instruccion instr = null;
    List<Instruccion> instruccionesIfTrue = new ArrayList<Instruccion>();
    List<Instruccion> instruccionesIfFalse = new ArrayList<Instruccion>();
}
{
    <IF> <PARENI> expBool = expbool() <PAREND> <LLAVEI> (instr = instruccion() {instruccionesIfTrue.add(instr);} )+  <LLAVED>
    <ELSE> <LLAVEI> (instr = instruccion() {instruccionesIfFalse.add(instr);} )+ <LLAVED>
    {
        return new If(expBool, instruccionesIfTrue, instruccionesIfFalse);
    }
}

While mientras() :
{
    ExpBoolean expBool = null;
    Instruccion instr = null;
    List<Instruccion> instrucciones = new ArrayList<Instruccion>();
}
{
    <WHILE> <PARENI> expBool = expbool() <PAREND> <LLAVEI> (instr = instruccion() {instrucciones.add(instr);} )+  <LLAVED>
    {
        return new While(expBool, instrucciones);
    }
}

Expresion exp():
{
    Expresion op1;
    Expresion op2;
    Expresion exp = null;
}
{
    op1=factor()
    (
        <MAS> op2=factor() { exp = new ExpBinaria(op1, op2, ExpBinaria.Operador.SUMA); }
        |
        <MENOS> op2=factor() { exp = new ExpBinaria(op1, op2, ExpBinaria.Operador.RESTA); }
    )*
    {
        if (exp == null) {
            return op1;
        } else {
            return exp;
        }
    }
}

Expresion factor():
{
    Expresion op1;
    Expresion op2;
    Expresion exp = null;
}
{
    op1=num()
    (
        <POR> op2=num() { exp = new ExpBinaria(op1, op2, ExpBinaria.Operador.POR); }
        |
        <DIV> op2=num() { exp = new ExpBinaria(op1, op2, ExpBinaria.Operador.DIV); }
    )*
    {
        if (exp == null) {
            return op1;
        } else {
            return exp;
        }
    }
}

Expresion num() :
{}
{
     <NUM> {return new ExpNum(Integer.parseInt(token.image));}
     |
     <ID> { return new ExpId(token.image); }
}


ExpBoolean expbool():
{
    Expresion op1;
    Expresion op2;
    ExpBoolean.Operador op = null;
}
{
    op1=exp()
    (
        <GT> op2=exp() { op = ExpBoolean.Operador.GT; }
        |
        <LT> op2=exp() { op = ExpBoolean.Operador.LT; }
    )
    {
        return new ExpBoolean(op1, op2, op);
    }
}
















































